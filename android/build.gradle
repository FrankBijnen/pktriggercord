apply plugin: 'com.android.application'

gradle.taskGraph.whenReady { taskGraph ->
    if(taskGraph.hasTask(':assembleRelease')) {
        def storePassword
        def keyPassword
        storePassword = System.console().readPassword("\n\n\nEnter keystore password: ")
        storePassword = new String(storePassword)

        if(storePassword.size() <= 0) {
            throw new InvalidUserDataException("Empty keystore password")
        }

        keyPassword = System.console().readPassword("\nEnter key password: ")
        keyPassword = new String(keyPassword)

        if(keyPassword.size() <= 0) {
            throw new InvalidUserDataException("Empty key password")
        }

        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyPassword = keyPassword
    }
}

clean {
    delete 'assets'
    delete 'libs'
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    externalNativeBuild {
        ndkBuild {
            path "jni/Android.mk"
        }
    }

    sourceSets {          
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    defaultConfig {
        applicationId "info.melda.sala.pktriggercord"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 8501
        versionName "0.85.01"
        setProperty("archivesBaseName", "info.melda.sala.pktriggercord.PkTriggerCord-$versionName" )
        externalNativeBuild {
            ndkBuild {
                abiFilters "armeabi-v7a"
                targets "pktriggercord-cli"
                arguments  "V=1 install"
            }
        }
    }

    signingConfigs {
        release {
            storeFile file("${System.properties['user.home']}/progs/java_keystore_google_play.jks")
            storePassword ''
            keyAlias 'melda'
            keyPassword ''
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api 'com.android.support:appcompat-v7:28.0.0'
}

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }
}
